---
title: 2.a2 Hellos intel_syntax
permalink: /hellos-intel-syntax
---


# hello.s intel_syntax

```asm
1        .file   "hello.c"
2        .intel_syntax noprefix
3        .def    ___main;        .scl    2;      .type   32;     .endef
4        .section .rdata,"dr"
5        .align 4
6   LC0:
7        .ascii "Hello World, do you remember me?\0"
8        .text
9        .globl  _main
10       .def    _main;  .scl    2;      .type   32;     .endef
11  _main:
12        push    ebp
13        mov     ebp, esp
14        and     esp, -16
15        sub     esp, 16
16        call    ___main
17        mov     DWORD PTR [esp], OFFSET FLAT:LC0
18        call    _puts
19        mov     eax, 0
20        leave
21        ret
22        .ident  "GCC: (tdm-1) 5.1.0"
23        .def    _puts;  .scl    2;      .type   32;     .endef
```
The following lines will be omitted from our analysis, but for sake of completeness here is a brief overview of their purpose.

Line 1 .file "original source filename", it creates a symbol table entry where filename is the symbol name and STT_FILE is the symbol table type, used for debugging.

Line 2, shows the assembly code is in intel_syntax noprefix format (versus AT&T syntax).

Line 3, 10, and 23 Begin defining debugging information for a symbol name; the definition extends until the .endef directive is encountered.

Line 22: The .ident directive creates an entry in the .comment section in order to save the information about the compiler in the object file.

Below is the result of our code trimming, which is the "functional" part of our program.

```asm
1        .section .rdata,"dr"
2        .align 4
3   LC0:
4        .ascii "Hello World, do you remember me?\0"
5        .text
6        .globl  _main
7  _main:
8         push    ebp
9         mov     ebp, esp
10        and     esp, -16
11        sub     esp, 16
12        call    ___main
13        mov     DWORD PTR [esp], OFFSET FLAT:LC0
14        call    _puts
15        mov     eax, 0
16        leave
17        ret
```
Note - The Intel assembler uses the opposite order (destination‚Üêsource) for operands.

Assembler directives are commands that begin with a period (.), they are part of the assembler syntax but are not related to the x86 processor instruction set.

Line 1: .section directive declares the start of a section of code.

Line 2: .align integer directive causes the next data generated to be aligned modulo integer bytes. Integer must be positive and must be a power of 2.

Line 3: declares a label "LC0", then places some raw ASCII text (line 4) into the program, starting at the label's location.

Line 4: .ascii "string" directive places the characters in string into the object module at the current location. \0 specifies a null character at the end of the string.

Line 5: .text directive defines the current section as .text, by itself this tells the assembler that the following code goes in the default section,

Line 6: .globl directive declares \_main as a global label, which allows other parts of the program to see it.

Line 7: declares the \_main label, marking the place that is called from the startup code.

Line 8: push the value of ebp onto the stack, ebp is used as the base register to reference the local variables.

Line 9: move data from esp to ebp (value of esp is moved to ebp).

Line 10: bitwise logical AND's ESP with 0xFFFFFFF0, aligning the stack with the next lowest 16-byte boundary.

Line 11: 16 is subtracted from ESP to reserve space on the stack (since the Intel stack grows from higher memory locations to lower ones). In this case, 16 bytes have been reserved on the stack.

Line 12: calls function \__main in the C library.

Line 13: move data from OFFSET FLAT:LC0 to DWORD PTR [esp] moves the location of the ASCII string to the top of the stack.

Line 14: calls the \_puts subroutine in the C library to print the message to the console.

Line 15: move 0 into eax register - stores our return value 0, in eax. The C calling convention is to store return values in eax when exiting a routine.

Line 16: frees the space saved on the stack by copying ebp into esp, then popping the saved value of ebp back to ebp.

Line 17: returns control to the calling procedure by popping the saved instruction pointer from the stack.
